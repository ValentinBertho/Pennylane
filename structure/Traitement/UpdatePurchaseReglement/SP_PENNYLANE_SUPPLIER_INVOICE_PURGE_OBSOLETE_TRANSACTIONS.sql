IF EXISTS (SELECT * FROM sys.objects WHERE object_id = object_id(N'[SP_PENNYLANE_SUPPLIER_INVOICE_PURGE_OBSOLETE_TRANSACTIONS]') AND is_ms_shipped = 0 AND [type] IN ('P'))
DROP PROCEDURE [SP_PENNYLANE_SUPPLIER_INVOICE_PURGE_OBSOLETE_TRANSACTIONS]
GO

SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO

CREATE PROCEDURE [SP_PENNYLANE_SUPPLIER_INVOICE_PURGE_OBSOLETE_TRANSACTIONS]
    @INVOICE_ID VARCHAR(50),
    @VALID_TRANSACTION_IDS_CSV VARCHAR(MAX), -- Ex: '1,2,3,4'
    @RESULT_OUTPUT INT OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
    BEGIN TRY

        DECLARE @NO_V_FACTURE INT;
        SET @NO_V_FACTURE = (SELECT NO_V_FACTURE FROM V_FACTURE WHERE PENNYLANE_ID = @INVOICE_ID);
        IF @NO_V_FACTURE IS NULL
        BEGIN
            SET @RESULT_OUTPUT = -2; -- Facture non trouvée
            RETURN;
        END

        -- Création d'une table temporaire pour stocker les IDs valides
        DECLARE @TEMP_IDS TABLE (ID BIGINT);
        DECLARE @POS INT = 1;
        DECLARE @NEXT_POS INT;
        DECLARE @ID_STR VARCHAR(100);

        WHILE @POS <= LEN(@VALID_TRANSACTION_IDS_CSV)
        BEGIN
            SET @NEXT_POS = CHARINDEX(',', @VALID_TRANSACTION_IDS_CSV, @POS);
            IF @NEXT_POS = 0 SET @NEXT_POS = LEN(@VALID_TRANSACTION_IDS_CSV) + 1;
            SET @ID_STR = SUBSTRING(@VALID_TRANSACTION_IDS_CSV, @POS, @NEXT_POS - @POS);
            IF @ID_STR <> ''
                INSERT INTO @TEMP_IDS (ID) VALUES (CAST(@ID_STR AS BIGINT));
            SET @POS = @NEXT_POS + 1;
        END

        -- Suppression des transactions obsolètes
        DELETE FROM REGLEMENT
        WHERE NO_V_FACTURE = @NO_V_FACTURE
          AND ID_TRANSACTION_PENNYLANE IS NOT NULL
          AND ID_TRANSACTION_PENNYLANE NOT IN (SELECT ID FROM @TEMP_IDS);

        SET @RESULT_OUTPUT = 1; -- Succès

    END TRY
    BEGIN CATCH
        DECLARE @PARAMS_LOG NVARCHAR(MAX);

        SET @PARAMS_LOG =
            'INVOICE_ID=' + ISNULL(@INVOICE_ID, 'NULL') +
            ', VALID_TRANSACTION_IDS_CSV=' + ISNULL(@VALID_TRANSACTION_IDS_CSV, 'NULL');

        EXEC SP_INSERT_ERROR_LOG
            @PROCEDURE_NAME = 'SP_PENNYLANE_SUPPLIER_INVOICE_PURGE_OBSOLETE_TRANSACTIONS',
            @PARAMETERS = @PARAMS_LOG;

        SET @RESULT_OUTPUT = -99;
    END CATCH
END
GO
